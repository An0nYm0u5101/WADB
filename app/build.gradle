apply plugin: 'com.android.application'

def localProps = new Properties()
localProps.load(new FileInputStream(rootProject.file('local.properties')))

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        applicationId "moe.haruue.wadb"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "github_url", "https://github.com/RikkaApps/WADB"
        resValue "string", "copyright", "Copyright Â© Haruue Icymoon, PinkD, Rikka"
        resConfigs 'de', 'it', 'zh-rCN', 'zh-rHK', 'zh-rTW'

        buildConfigField "boolean", "DONOT_RESTART_ADBD", "${localProps.get("debug.dontRestartAdbd", false)}" // set it true to prevent restart adbd for DEBUG
    }
    signingConfigs {
        sign
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
    }
    packagingOptions {
        exclude '/META-INF/*.version'
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "wadb-v${variant.versionName}.apk"

        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assembleProvider.get().doLast {
                copy {
                    from variant.mappingFile
                    into "release"
                    rename { String fileName ->
                        "mapping-${variant.versionCode}.txt"
                    }
                }
                copy {
                    from outputFile
                    into "release"
                }
            }
        }
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://dl.bintray.com/haruue/maven/' }
    maven { url 'https://dl.bintray.com/rikkaw/Libraries' }
    maven { url 'https://dl.bintray.com/rikkaw/Shizuku' }
    mavenLocal()
}

dependencies {
    def preferenceVersion = '4.0.0'
    def libsuVersion = '2.5.0'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    compileOnly project(':hidden')

    implementation "androidx.core:core:1.2.0-alpha04"
    implementation "androidx.fragment:fragment:1.2.0-alpha03"
    implementation "androidx.recyclerview:recyclerview:1.1.0-beta04"
    implementation 'androidx.browser:browser:1.2.0-alpha07'

    implementation "moe.shizuku.support:design:3.1.11"
    implementation "moe.shizuku.support:support-utils:3.0.6"
    implementation "moe.shizuku.preference:preference-android:$preferenceVersion"
    implementation "moe.shizuku.preference:preference-simplemenu-android:$preferenceVersion"

    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"

    implementation 'moe.shizuku.privilege:api:3.1.0-alpha1'
}


def signFile = rootProject.file('signing.properties')
def signProps = new Properties()
if (signFile.canRead()) {
    signProps.load(new FileInputStream(signFile))
    if (signProps != null) {
        android.signingConfigs.sign.storeFile file(signProps['KEYSTORE_FILE'])
        android.signingConfigs.sign.storePassword signProps['KEYSTORE_PASSWORD']
        android.signingConfigs.sign.keyAlias signProps['KEYSTORE_ALIAS']
        android.signingConfigs.sign.keyPassword signProps['KEYSTORE_ALIAS_PASSWORD']
    } else {
        println 'some entries in \'signing.properties\' not found!'
    }
} else {
    println '\'signing.properties\' not found!'
}

