apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        applicationId "moe.haruue.wadb"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "github_url", "https://github.com/RikkaApps/WADB"
        resValue "string", "copyright", "Copyright Â© Haruue Icymoon, PinkD, Rikka"

        buildConfigField "boolean", "DONOT_RESTART_ADBD", "true" // set it true to prevent restart adbd for DEBUG
    }
    signingConfigs {
        sign
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "wadb-v${variant.versionName}.apk"

        /*if (variant.getBuildType().isMinifyEnabled()) {
            variant.assembleProvider.get().doLast {
                copy {
                    from variant.getMappingFileProvider().get()
                    into "${rootDir}/${project.name}/release"
                    rename { String fileName ->
                        "mapping-${variant.versionCode}.txt"
                    }
                }
                copy {
                    from variant.outputs*.outputFile
                    into "${rootDir}/${project.name}/release"
                }
            }
        }*/
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://dl.bintray.com/haruue/maven/' }
    maven { url 'https://dl.bintray.com/rikkaw/Libraries' }
    mavenLocal()
}

dependencies {
    def preferenceVersion = '4.0.0'
    def libsuVersion = '2.5.0'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    compileOnly project(':hidden')

    implementation "androidx.core:core:1.2.0-alpha03"
    implementation "androidx.fragment:fragment:1.2.0-alpha02"
    implementation "androidx.recyclerview:recyclerview:1.1.0-beta03"
    implementation 'androidx.browser:browser:1.2.0-alpha07'

    implementation "moe.shizuku.support:design:3.1.11"
    implementation "moe.shizuku.support:support-utils:3.0.6"
    implementation "moe.shizuku.preference:preference:$preferenceVersion"
    implementation "moe.shizuku.preference:preference-dialog-android:$preferenceVersion"
    implementation "moe.shizuku.preference:preference-simplemenu:$preferenceVersion"

    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
}


def signFile = rootProject.file('signing.properties')
def signProps = new Properties()
if (signFile.canRead()) {
    signProps.load(new FileInputStream(signFile))
    if (signProps != null) {
        android.signingConfigs.sign.storeFile file(signProps['KEYSTORE_FILE'])
        android.signingConfigs.sign.storePassword signProps['KEYSTORE_PASSWORD']
        android.signingConfigs.sign.keyAlias signProps['KEYSTORE_ALIAS']
        android.signingConfigs.sign.keyPassword signProps['KEYSTORE_ALIAS_PASSWORD']
    } else {
        println 'some entries in \'signing.properties\' not found!'
    }
} else {
    println '\'signing.properties\' not found!'
}

